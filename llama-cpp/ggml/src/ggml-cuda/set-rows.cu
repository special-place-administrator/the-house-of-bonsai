#include "set-rows.cuh"
#include "cpy-utils.cuh"
#include "rq-quant-cuda.cuh"
#include <cstring>
#include <cerrno>

static void load_rq4_alpha() {
    static bool loaded = false;
    if (loaded) return;
    loaded = true;
    const char *s = getenv("RQ4_ALPHA");
    if (!s) return;
    char *end;
    errno = 0;
    float a = strtof(s, &end);
    if (end == s || errno != 0 || a <= 0.0f || a >= 10.0f) {
        fprintf(stderr, "RQ4: invalid RQ4_ALPHA='%s'\n", s);
    } else {
        cudaMemcpyToSymbol(d_rq4_alpha, &a, sizeof(float));
        fprintf(stderr, "RQ4: alpha=%.3f\n", a);
    }
}

static void load_tcq_norm_alpha() {
    static bool loaded = false;
    if (loaded) return;
    loaded = true;

    // Context-adaptive decode-time alpha is the default. Force encode-time V alpha to 1.0
    // unless RQ_ISO_ENCODE_ALPHA=1 is explicitly set to use encode-time alpha instead.
    const char *encode_mode = getenv("RQ_ISO_ENCODE_ALPHA");
    if (!encode_mode) {
        float one = 1.0f;
        cudaMemcpyToSymbol(d_tcq_norm_alpha_v, &one, sizeof(float));
        fprintf(stderr, "TCQ: encode V alpha=1.0 (context-adaptive decode-time alpha active)\n");
        // Still allow K alpha override
        const char *s = getenv("RQ_ISO_ALPHA");
        if (s) {
            char *end;
            errno = 0;
            float a = strtof(s, &end);
            if (end != s && errno == 0 && a > 0.0f && a < 10.0f) {
                cudaMemcpyToSymbol(d_tcq_norm_alpha, &a, sizeof(float));
            }
        }
        return;
    }

    const char *s = getenv("RQ_ISO_ALPHA");
    const char *sv = getenv("RQ_ISO_ALPHA_V");
    if (!s && !sv) return;
    float alpha_k = 1.0f;
    bool k_set = false;
    if (s) {
        char *end;
        errno = 0;
        float a = strtof(s, &end);
        if (end == s || errno != 0 || a <= 0.0f || a >= 10.0f) {
            fprintf(stderr, "TCQ: invalid RQ_ISO_ALPHA='%s'\n", s);
        } else {
            alpha_k = a;
            k_set = true;
            cudaMemcpyToSymbol(d_tcq_norm_alpha, &alpha_k, sizeof(float));
        }
    }
    if (sv) {
        char *end;
        errno = 0;
        float a = strtof(sv, &end);
        if (end == sv || errno != 0 || a <= 0.0f || a >= 10.0f) {
            fprintf(stderr, "TCQ: invalid RQ_ISO_ALPHA_V='%s'\n", sv);
        } else {
            cudaMemcpyToSymbol(d_tcq_norm_alpha_v, &a, sizeof(float));
            fprintf(stderr, "TCQ: norm alpha K=%.3f V=%.3f\n", alpha_k, a);
            return;
        }
    }
    // RQ_ISO_ALPHA set but not RQ_ISO_ALPHA_V: V matches K for backwards compat
    if (k_set) {
        cudaMemcpyToSymbol(d_tcq_norm_alpha_v, &alpha_k, sizeof(float));
        fprintf(stderr, "TCQ: norm alpha K=V=%.3f\n", alpha_k);
    }
}

// TCQ error dump for autocorrelation analysis (RQ_ISO_DUMP_ERRORS=N)
static int    tcq_dump_n = 0;
static float * tcq_dump_x_host = nullptr;
static uint8_t * tcq_dump_out_host = nullptr;
static float * tcq_dump_x_dev = nullptr;
static uint8_t * tcq_dump_out_dev = nullptr;

static void tcq_error_dump_flush() {
    if (tcq_dump_n == 0) return;
    cudaMemcpy(tcq_dump_x_host, tcq_dump_x_dev, tcq_dump_n * 128 * sizeof(float), cudaMemcpyDeviceToHost);
    cudaMemcpy(tcq_dump_out_host, tcq_dump_out_dev, tcq_dump_n * 128 * sizeof(uint8_t), cudaMemcpyDeviceToHost);
    FILE * f = fopen("/tmp/tcq_errors.bin", "wb");
    if (f) {
        int32_t header[1] = { tcq_dump_n };
        fwrite(header, sizeof(int32_t), 1, f);
        fwrite(tcq_dump_x_host, sizeof(float), tcq_dump_n * 128, f);
        fwrite(tcq_dump_out_host, sizeof(uint8_t), tcq_dump_n * 128, f);
        fclose(f);
        fprintf(stderr, "TCQ: dumped %d groups to /tmp/tcq_errors.bin\n", tcq_dump_n);
    }
    cudaFree(tcq_dump_x_dev);
    cudaFree(tcq_dump_out_dev);
    free(tcq_dump_x_host);
    free(tcq_dump_out_host);
}

static void init_tcq_error_dump() {
    static bool loaded = false;
    if (loaded) return;
    loaded = true;
    const char *s = getenv("RQ_ISO_DUMP_ERRORS");
    if (!s) return;
    int n = atoi(s);
    if (n <= 0 || n > 500000) return;
    tcq_dump_n = n;
    tcq_dump_x_host = (float *)malloc(n * 128 * sizeof(float));
    tcq_dump_out_host = (uint8_t *)malloc(n * 128 * sizeof(uint8_t));
    cudaMalloc(&tcq_dump_x_dev, n * 128 * sizeof(float));
    cudaMalloc(&tcq_dump_out_dev, n * 128 * sizeof(uint8_t));
    cudaMemcpyToSymbol(d_tcq_dump_x_buf, &tcq_dump_x_dev, sizeof(float*));
    cudaMemcpyToSymbol(d_tcq_dump_out_buf, &tcq_dump_out_dev, sizeof(uint8_t*));
    cudaMemcpyToSymbol(d_tcq_dump_max, &n, sizeof(int));
    atexit(tcq_error_dump_flush);
    fprintf(stderr, "TCQ: will dump errors for first %d groups to /tmp/tcq_errors.bin\n", n);
}

typedef void (*set_rows_kernel_t)(const char * src, char * dst);

// Generic quantized set_rows kernel template
template <typename idx_t, typename block_type, int qk, void (*quantize_func)(const float *, block_type *)>
static __global__ void k_set_rows_quant(const float * __restrict__ src0,
                                        const idx_t * __restrict__ src1,
                                        block_type * __restrict__ dst,
                                        const int64_t ne_total,
                                        const int64_t ne10,
                                        const int64_t ne11,
                                        const int64_t ne12,
                                        const int64_t ne13,
                                        const int64_t s01,
                                        const int64_t s02,
                                        const int64_t s03,
                                        const int64_t s10,
                                        const int64_t s11,
                                        const int64_t s12,
                                        const int64_t s1,
                                        const int64_t s2,
                                        const int64_t s3,
                                        const uint3   ne00,
                                        const uint3   ne01,
                                        const uint3   ne02,
                                        const uint3   ne11_fd,
                                        const uint3   ne12_fd) {
    const int64_t i = int64_t(blockDim.x) * blockIdx.x + threadIdx.x;

    if (i >= ne_total) {
        return;
    }

    const int64_t i_base = i * qk;
    uint32_t      tmp    = (uint32_t) i_base;
    uint2         div_mod;

    div_mod           = fast_div_modulo(tmp, ne00);
    const int64_t i00 = div_mod.y;
    tmp               = div_mod.x;

    div_mod           = fast_div_modulo(tmp, ne01);
    const int64_t i01 = div_mod.y;
    tmp               = div_mod.x;

    div_mod           = fast_div_modulo(tmp, ne02);
    const int64_t i02 = div_mod.y;
    const int64_t i03 = div_mod.x;

    const int64_t i12 = fastmodulo((uint32_t) i03, ne12_fd);
    const int64_t i11 = fastmodulo((uint32_t) i02, ne11_fd);
    const int64_t i10 = i01;

    const int64_t dst_row = *(src1 + i10*s10 + i11*s11 + i12*s12);

    const float * src0_row = src0 + i01*s01 + i02*s02 + i03*s03;
    block_type * dst_row_ptr = dst + (dst_row*s1 + i02*s2 + i03*s3) / sizeof(block_type);

    const float * src_block = src0_row + i00;
    block_type * dst_block = dst_row_ptr + i00 / qk;

    quantize_func(src_block, dst_block);

    GGML_UNUSED(ne10);
    GGML_UNUSED(ne11);
    GGML_UNUSED(ne12);
    GGML_UNUSED(ne13);
}

// Template dispatch function for quantized set_rows
template<typename idx_t, typename block_type, int qk, void (*quantize_func)(const float*, block_type*)>
static void set_rows_cuda_quant(
        const float * src0_d, const idx_t * src1_d, block_type * dst_d,
        const int64_t ne00, const int64_t ne01, const int64_t ne02, const int64_t ne03,
        const int64_t ne10, const int64_t ne11, const int64_t ne12, const int64_t ne13,
        const size_t nb01, const size_t nb02, const size_t nb03,
        const size_t nb10, const size_t nb11, const size_t nb12,
        const size_t nb1, const size_t nb2, const size_t nb3,
        cudaStream_t stream) {

    GGML_ASSERT(ne00 % qk == 0);
    const int64_t ne_total = (ne00 * ne01 * ne02 * ne03) / qk;
    const int num_blocks = (ne_total + CUDA_SET_ROWS_BLOCK_SIZE - 1) / CUDA_SET_ROWS_BLOCK_SIZE;
    const dim3 block_size(CUDA_SET_ROWS_BLOCK_SIZE);
    const dim3 grid_size(num_blocks);

    const int64_t s01 = nb01/sizeof(float);
    const int64_t s02 = nb02/sizeof(float);
    const int64_t s03 = nb03/sizeof(float);
    const int64_t s10 = nb10/sizeof(idx_t);
    const int64_t s11 = nb11/sizeof(idx_t);
    const int64_t s12 = nb12/sizeof(idx_t);
    const int64_t s1  = nb1;
    const int64_t s2  = nb2;
    const int64_t s3  = nb3;

    if (ne_total > 0 && ne00 > 0 && ne01 > 0 && ne02 > 0 && ne11 > 0 && ne12 > 0) {
        const uint3 ne00_fd = init_fastdiv_values((uint32_t) ne00);
        const uint3 ne01_fd = init_fastdiv_values((uint32_t) ne01);
        const uint3 ne02_fd = init_fastdiv_values((uint32_t) ne02);
        const uint3 ne11_fd = init_fastdiv_values((uint32_t) ne11);
        const uint3 ne12_fd = init_fastdiv_values((uint32_t) ne12);

        k_set_rows_quant<idx_t, block_type, qk, quantize_func><<<grid_size, block_size, 0, stream>>>(
            src0_d, src1_d, dst_d, ne_total, ne10, ne11, ne12, ne13, s01, s02, s03, s10, s11, s12, s1, s2, s3, ne00_fd,
            ne01_fd, ne02_fd, ne11_fd, ne12_fd);
    }
}

template <typename src_t, typename idx_t, typename dst_t>
static __global__ void k_set_rows(const src_t * __restrict__ src0,
                                  const idx_t * __restrict__ src1,
                                  dst_t * __restrict__ dst,
                                  const int64_t ne_total,
                                  const int64_t ne10,
                                  const int64_t ne11,
                                  const int64_t ne12,
                                  const int64_t ne13,
                                  const int64_t s01,
                                  const int64_t s02,
                                  const int64_t s03,
                                  const int64_t s10,
                                  const int64_t s11,
                                  const int64_t s12,
                                  const int64_t s1,
                                  const int64_t s2,
                                  const int64_t s3,
                                  const uint3   ne00,
                                  const uint3   ne01,
                                  const uint3   ne02,
                                  const uint3   ne11_fd,
                                  const uint3   ne12_fd) {
    const int64_t i = int64_t(blockDim.x) * blockIdx.x + threadIdx.x;

    if (i >= ne_total) {
        return;
    }

    uint32_t tmp = (uint32_t) i;
    uint2    div_mod;

    div_mod           = fast_div_modulo(tmp, ne00);
    const int64_t i00 = div_mod.y;
    tmp               = div_mod.x;

    div_mod           = fast_div_modulo(tmp, ne01);
    const int64_t i01 = div_mod.y;
    tmp               = div_mod.x;

    div_mod           = fast_div_modulo(tmp, ne02);
    const int64_t i02 = div_mod.y;
    const int64_t i03 = div_mod.x;

    const int64_t i12 = fastmodulo((uint32_t) i03, ne12_fd);
    const int64_t i11 = fastmodulo((uint32_t) i02, ne11_fd);
    const int64_t i10 = i01;

    const int64_t dst_row = *(src1 + i10*s10 + i11*s11 + i12*s12);

    const src_t * src0_row = src0 + i01*s01 + i02*s02 + i03*s03;
    dst_t * dst_row_ptr    = dst + dst_row*s1 + i02*s2 + i03*s3;

    dst_row_ptr[i00] = ggml_cuda_cast<dst_t>(src0_row[i00]);

    GGML_UNUSED(ne10);
    GGML_UNUSED(ne11);
    GGML_UNUSED(ne12);
    GGML_UNUSED(ne13);
}

template<typename src_t, typename idx_t, typename dst_t>
static void set_rows_cuda(
        const src_t * src0_d, const idx_t * src1_d, dst_t * dst_d,
        const int64_t ne00, const int64_t ne01, const int64_t ne02, const int64_t ne03,
        const int64_t ne10, const int64_t ne11, const int64_t ne12, const int64_t ne13,
        const size_t nb01, const size_t nb02, const size_t nb03,
        const size_t nb10, const size_t nb11, const size_t nb12,
        const size_t nb1, const size_t nb2, const size_t nb3,
        cudaStream_t stream) {

    const int64_t ne_total = ne00 * ne01 * ne02 * ne03;
    const int num_blocks = (ne_total + CUDA_SET_ROWS_BLOCK_SIZE - 1) / CUDA_SET_ROWS_BLOCK_SIZE;
    const dim3 block_size(CUDA_SET_ROWS_BLOCK_SIZE);
    const dim3 grid_size(num_blocks);


    const int64_t s01 = nb01/sizeof(src_t);
    const int64_t s02 = nb02/sizeof(src_t);
    const int64_t s03 = nb03/sizeof(src_t);
    const int64_t s10 = nb10/sizeof(idx_t);
    const int64_t s11 = nb11/sizeof(idx_t);
    const int64_t s12 = nb12/sizeof(idx_t);
    const int64_t s1  = nb1/sizeof(dst_t);
    const int64_t s2  = nb2/sizeof(dst_t);
    const int64_t s3  = nb3/sizeof(dst_t);

    if (ne_total > 0 && ne00 > 0 && ne01 > 0 && ne02 > 0 && ne11 > 0 && ne12 > 0) {
        const uint3 ne00_fd = init_fastdiv_values((uint32_t) ne00);
        const uint3 ne01_fd = init_fastdiv_values((uint32_t) ne01);
        const uint3 ne02_fd = init_fastdiv_values((uint32_t) ne02);
        const uint3 ne11_fd = init_fastdiv_values((uint32_t) ne11);
        const uint3 ne12_fd = init_fastdiv_values((uint32_t) ne12);

        k_set_rows<<<grid_size, block_size, 0, stream>>>(src0_d, src1_d, dst_d, ne_total, ne10, ne11, ne12, ne13, s01,
                                                         s02, s03, s10, s11, s12, s1, s2, s3, ne00_fd, ne01_fd, ne02_fd,
                                                         ne11_fd, ne12_fd);
    }
}

// Global backtrace buffer for Viterbi (replaces 32KB shared memory per block)
static uint8_t * tcq_bt_buf = nullptr;
static int64_t   tcq_bt_buf_bytes = 0;

static void ensure_tcq_bt_buf(int64_t bytes_needed) {
    if (bytes_needed <= tcq_bt_buf_bytes) return;
    if (tcq_bt_buf) cudaFree(tcq_bt_buf);
    cudaMalloc(&tcq_bt_buf, bytes_needed);
    tcq_bt_buf_bytes = bytes_needed;
}

template<typename src_t, typename idx_t>
static void set_rows_cuda(ggml_backend_cuda_context & ctx, const ggml_tensor * src0, const ggml_tensor * src1, ggml_tensor * dst) {
    const src_t * src0_d = (const src_t *)src0->data;
    const idx_t * src1_d = (const idx_t *)src1->data;

    GGML_TENSOR_BINARY_OP_LOCALS

    cudaStream_t stream = ctx.stream();


    if (dst->type == GGML_TYPE_F32) {
        set_rows_cuda(
            src0_d, src1_d, (float*)dst->data,
            ne00, ne01, ne02, ne03,
            ne10, ne11, ne12, ne13,
            nb01, nb02, nb03,
            nb10, nb11, nb12,
            nb1, nb2, nb3,
            stream
        );
    } else if (dst->type == GGML_TYPE_F16) {
        set_rows_cuda(
            src0_d, src1_d, (half*)dst->data,
            ne00, ne01, ne02, ne03,
            ne10, ne11, ne12, ne13,
            nb01, nb02, nb03,
            nb10, nb11, nb12,
            nb1, nb2, nb3,
            stream
        );
    } else if (dst->type == GGML_TYPE_BF16) {
        set_rows_cuda(
            src0_d, src1_d, (nv_bfloat16*)dst->data,
            ne00, ne01, ne02, ne03,
            ne10, ne11, ne12, ne13,
            nb01, nb02, nb03,
            nb10, nb11, nb12,
            nb1, nb2, nb3,
            stream
        );
    } else if (dst->type == GGML_TYPE_Q4_0) {
        set_rows_cuda_quant<idx_t, block_q4_0, QK4_0, quantize_f32_q4_0_block>(
            src0_d, src1_d, (block_q4_0*)dst->data,
            ne00, ne01, ne02, ne03,
            ne10, ne11, ne12, ne13,
            nb01, nb02, nb03,
            nb10, nb11, nb12,
            nb1, nb2, nb3,
            stream
        );
    } else if (dst->type == GGML_TYPE_Q4_1) {
        set_rows_cuda_quant<idx_t, block_q4_1, QK4_1, quantize_f32_q4_1_block>(
            src0_d, src1_d, (block_q4_1*)dst->data,
            ne00, ne01, ne02, ne03,
            ne10, ne11, ne12, ne13,
            nb01, nb02, nb03,
            nb10, nb11, nb12,
            nb1, nb2, nb3,
            stream
        );
    } else if (dst->type == GGML_TYPE_Q5_0) {
        set_rows_cuda_quant<idx_t, block_q5_0, QK5_0, quantize_f32_q5_0_block>(
            src0_d, src1_d, (block_q5_0*)dst->data,
            ne00, ne01, ne02, ne03,
            ne10, ne11, ne12, ne13,
            nb01, nb02, nb03,
            nb10, nb11, nb12,
            nb1, nb2, nb3,
            stream
        );
    } else if (dst->type == GGML_TYPE_Q5_1) {
        set_rows_cuda_quant<idx_t, block_q5_1, QK5_1, quantize_f32_q5_1_block>(
            src0_d, src1_d, (block_q5_1*)dst->data,
            ne00, ne01, ne02, ne03,
            ne10, ne11, ne12, ne13,
            nb01, nb02, nb03,
            nb10, nb11, nb12,
            nb1, nb2, nb3,
            stream
        );
    } else if (dst->type == GGML_TYPE_Q8_0) {
        set_rows_cuda_quant<idx_t, block_q8_0, QK8_0, quantize_f32_q8_0_block>(
            src0_d, src1_d, (block_q8_0*)dst->data,
            ne00, ne01, ne02, ne03,
            ne10, ne11, ne12, ne13,
            nb01, nb02, nb03,
            nb10, nb11, nb12,
            nb1, nb2, nb3,
            stream
        );
    } else if (dst->type == GGML_TYPE_IQ4_NL) {
        set_rows_cuda_quant<idx_t, block_iq4_nl, QK4_NL, quantize_f32_iq4_nl_block>(
            src0_d, src1_d, (block_iq4_nl*)dst->data,
            ne00, ne01, ne02, ne03,
            ne10, ne11, ne12, ne13,
            nb01, nb02, nb03,
            nb10, nb11, nb12,
            nb1, nb2, nb3,
            stream
        );
    } else if (dst->type == GGML_TYPE_RQ2_0) {
        GGML_ASSERT(ne00 % QK_RQ2_GROUP == 0);
        const int64_t ne_total_groups = (ne00 * ne01 * ne02 * ne03) / QK_RQ2_GROUP;
        const int num_blocks_grid = (ne_total_groups + CUDA_SET_ROWS_BLOCK_SIZE - 1) / CUDA_SET_ROWS_BLOCK_SIZE;
        const int64_t s01_f = nb01/sizeof(float); const int64_t s02_f = nb02/sizeof(float); const int64_t s03_f = nb03/sizeof(float);
        const int64_t s10_i = nb10/sizeof(idx_t); const int64_t s11_i = nb11/sizeof(idx_t); const int64_t s12_i = nb12/sizeof(idx_t);
        if (ne_total_groups > 0 && ne00 > 0 && ne01 > 0 && ne02 > 0 && ne11 > 0 && ne12 > 0) {
            const uint3 ne00_fd = init_fastdiv_values((uint32_t) ne00);
            const uint3 ne01_fd = init_fastdiv_values((uint32_t) ne01);
            const uint3 ne02_fd = init_fastdiv_values((uint32_t) ne02);
            const uint3 ne11_fd = init_fastdiv_values((uint32_t) ne11);
            const uint3 ne12_fd = init_fastdiv_values((uint32_t) ne12);
            k_set_rows_rq2<idx_t><<<num_blocks_grid, CUDA_SET_ROWS_BLOCK_SIZE, 0, stream>>>(
                src0_d, src1_d, (block_rq2_0 *)dst->data,
                ne_total_groups, ne00, ne01, ne02, ne10, ne11, ne12, ne13,
                s01_f, s02_f, s03_f, s10_i, s11_i, s12_i, nb1, nb2, nb3,
                ne00_fd, ne01_fd, ne02_fd, ne11_fd, ne12_fd);
        }
    } else if (dst->type == GGML_TYPE_RQ3_0) {
        GGML_ASSERT(ne00 % QK_RQ3_GROUP == 0);
        const int64_t ne_total_groups = (ne00 * ne01 * ne02 * ne03) / QK_RQ3_GROUP;
        const int num_blocks_grid = (ne_total_groups + CUDA_SET_ROWS_BLOCK_SIZE - 1) / CUDA_SET_ROWS_BLOCK_SIZE;
        const int64_t s01_f = nb01/sizeof(float); const int64_t s02_f = nb02/sizeof(float); const int64_t s03_f = nb03/sizeof(float);
        const int64_t s10_i = nb10/sizeof(idx_t); const int64_t s11_i = nb11/sizeof(idx_t); const int64_t s12_i = nb12/sizeof(idx_t);
        const int iq_is_k = (strncmp(dst->name, "cache_k_", 8) == 0) ? 1 : 0;
        if (ne_total_groups > 0 && ne00 > 0 && ne01 > 0 && ne02 > 0 && ne11 > 0 && ne12 > 0) {
            const uint3 ne00_fd = init_fastdiv_values((uint32_t) ne00);
            const uint3 ne01_fd = init_fastdiv_values((uint32_t) ne01);
            const uint3 ne02_fd = init_fastdiv_values((uint32_t) ne02);
            const uint3 ne11_fd = init_fastdiv_values((uint32_t) ne11);
            const uint3 ne12_fd = init_fastdiv_values((uint32_t) ne12);
            k_set_rows_rq3<idx_t><<<num_blocks_grid, CUDA_SET_ROWS_BLOCK_SIZE, 0, stream>>>(
                src0_d, src1_d, (block_rq3_0 *)dst->data,
                ne_total_groups, ne00, ne01, ne02, ne10, ne11, ne12, ne13,
                s01_f, s02_f, s03_f, s10_i, s11_i, s12_i, iq_is_k, nb1, nb2, nb3,
                ne00_fd, ne01_fd, ne02_fd, ne11_fd, ne12_fd);
        }
    } else if (dst->type == GGML_TYPE_RQ4_0) {
        load_rq4_alpha();
        set_rows_cuda_quant<idx_t, block_rq4_0, QK_RQ4, quantize_f32_rq4_0_block>(
            src0_d, src1_d, (block_rq4_0*)dst->data,
            ne00, ne01, ne02, ne03, ne10, ne11, ne12, ne13,
            nb01, nb02, nb03, nb10, nb11, nb12, nb1, nb2, nb3, stream);
    } else if (dst->type == GGML_TYPE_RQ3_ISO) {
        GGML_ASSERT(ne00 % QK_RQ3_ISO == 0);
        const int64_t ne_total_groups = (ne00 * ne01 * ne02 * ne03) / QK_RQ3_ISO;
        // Runtime codebook loading: RQ_ISO_CB overrides compiled-in codebook
        static bool tcq_cb_loaded = false;
        if (!tcq_cb_loaded) {
            tcq_cb_loaded = true;
            const char *cb_path = getenv("RQ_ISO_CB");
            if (cb_path) {
                float cb[512];
                FILE *f = fopen(cb_path, "rb");
                if (f && fread(cb, sizeof(float), 512, f) == 512) {
                    fclose(f);
                    cudaMemcpyToSymbol(d_rq3_iso_codebook, cb, 512*sizeof(float));
                    fprintf(stderr, "TCQ encode: loaded codebook from %s\n", cb_path);
                } else {
                    if (f) fclose(f);
                    fprintf(stderr, "TCQ encode: FAILED to load codebook from %s\n", cb_path);
                }
            }
            load_tcq_norm_alpha();
            init_tcq_error_dump();
        }
        // TCQ Viterbi encode: 512 threads per block, global bt buffer (128×512 bytes/block)
        const int64_t s01_f = nb01/sizeof(float); const int64_t s02_f = nb02/sizeof(float); const int64_t s03_f = nb03/sizeof(float);
        const int64_t s10_i = nb10/sizeof(idx_t); const int64_t s11_i = nb11/sizeof(idx_t); const int64_t s12_i = nb12/sizeof(idx_t);
        const int iq_is_k = (strncmp(dst->name, "cache_k_", 8) == 0) ? 1 : 0;
        if (ne_total_groups > 0 && ne00 > 0 && ne01 > 0 && ne02 > 0 && ne11 > 0 && ne12 > 0) {
            ensure_tcq_bt_buf(ne_total_groups * 128 * 512);
            const uint3 ne00_fd = init_fastdiv_values((uint32_t) ne00);
            const uint3 ne01_fd = init_fastdiv_values((uint32_t) ne01);
            const uint3 ne02_fd = init_fastdiv_values((uint32_t) ne02);
            const uint3 ne11_fd = init_fastdiv_values((uint32_t) ne11);
            const uint3 ne12_fd = init_fastdiv_values((uint32_t) ne12);
            k_set_rows_rq3_iso<idx_t><<<(int)ne_total_groups, 512, 0, stream>>>(
                src0_d, src1_d, (block_rq3_iso *)dst->data,
                ne_total_groups, tcq_bt_buf, ne00, ne01, ne02, ne10, ne11, ne12, ne13,
                s01_f, s02_f, s03_f, s10_i, s11_i, s12_i, iq_is_k, nb1, nb2, nb3,
                ne00_fd, ne01_fd, ne02_fd, ne11_fd, ne12_fd);
        }
    } else if (dst->type == GGML_TYPE_RQ4_ISO) {
        GGML_ASSERT(ne00 % QK_RQ4_ISO == 0);
        const int64_t ne_total_groups = (ne00 * ne01 * ne02 * ne03) / QK_RQ4_ISO;
        // Runtime codebook loading: RQ_ISO_CB2 overrides compiled-in 2-bit codebook
        static bool tcq2_cb_loaded = false;
        if (!tcq2_cb_loaded) {
            tcq2_cb_loaded = true;
            const char *cb_path = getenv("RQ_ISO_CB2");
            if (cb_path) {
                float cb[256];
                FILE *f = fopen(cb_path, "rb");
                if (f && fread(cb, sizeof(float), 256, f) == 256) {
                    fclose(f);
                    cudaMemcpyToSymbol(d_rq4_iso_codebook, cb, 256*sizeof(float));
                    fprintf(stderr, "TCQ2 encode: loaded 2-bit codebook from %s\n", cb_path);
                } else {
                    if (f) fclose(f);
                    fprintf(stderr, "TCQ2 encode: FAILED to load codebook from %s\n", cb_path);
                }
            }
            load_tcq_norm_alpha();
            init_tcq_error_dump();
        }
        // 2-bit TCQ Viterbi encode: 256 threads per block, global bt buffer (128×256 bytes/block)
        const int64_t s01_f = nb01/sizeof(float); const int64_t s02_f = nb02/sizeof(float); const int64_t s03_f = nb03/sizeof(float);
        const int64_t s10_i = nb10/sizeof(idx_t); const int64_t s11_i = nb11/sizeof(idx_t); const int64_t s12_i = nb12/sizeof(idx_t);
        const int iq_is_k = (strncmp(dst->name, "cache_k_", 8) == 0) ? 1 : 0;
        if (ne_total_groups > 0 && ne00 > 0 && ne01 > 0 && ne02 > 0 && ne11 > 0 && ne12 > 0) {
            ensure_tcq_bt_buf(ne_total_groups * 128 * 256);
            const uint3 ne00_fd = init_fastdiv_values((uint32_t) ne00);
            const uint3 ne01_fd = init_fastdiv_values((uint32_t) ne01);
            const uint3 ne02_fd = init_fastdiv_values((uint32_t) ne02);
            const uint3 ne11_fd = init_fastdiv_values((uint32_t) ne11);
            const uint3 ne12_fd = init_fastdiv_values((uint32_t) ne12);
            k_set_rows_rq4_iso<idx_t><<<(int)ne_total_groups, 256, 0, stream>>>(
                src0_d, src1_d, (block_rq4_iso *)dst->data,
                ne_total_groups, tcq_bt_buf, ne00, ne01, ne02, ne10, ne11, ne12, ne13,
                s01_f, s02_f, s03_f, s10_i, s11_i, s12_i, iq_is_k, nb1, nb2, nb3,
                ne00_fd, ne01_fd, ne02_fd, ne11_fd, ne12_fd);
        }
    } else {
        GGML_ABORT("unsupported type %s", ggml_type_name(dst->type));
    }
}


// InnerQ calibration state machine (driven by RQ_INNERQ env var)
static int innerq_state = 0; // 0=uninit, 1=calibrating, 2=active, -1=disabled
static int innerq_tokens_seen = 0;
static constexpr int INNERQ_CALIBRATION_TOKENS = 100000; // count total set_rows tokens across all layers

void ggml_cuda_op_set_rows(ggml_backend_cuda_context & ctx, ggml_tensor * dst) {
    const ggml_tensor * src0 = dst->src[0];
    const ggml_tensor * src1 = dst->src[1];

    GGML_ASSERT(src0->type == GGML_TYPE_F32);
    GGML_ASSERT(src1->type == GGML_TYPE_I64 || src1->type == GGML_TYPE_I32);

    // Post-rotation extraction: one-time init
    if (h_extract_state == 0 && (dst->type == GGML_TYPE_RQ3_0 || dst->type == GGML_TYPE_RQ4_0 || dst->type == GGML_TYPE_RQ3_ISO || dst->type == GGML_TYPE_RQ4_ISO)) {
        static const char * env = getenv("RQ_EXTRACT");
        if (env && atoi(env) > 0) {
            rq_extract_init(atoi(env));
        } else {
            h_extract_state = -1;
        }
    }
    // Check if extraction buffer is full
    if (h_extract_state == 1) rq_extract_check_done();

    // InnerQ: one-time init on first rq SET_ROWS call
    if (innerq_state == 0 && (dst->type == GGML_TYPE_RQ2_0 || dst->type == GGML_TYPE_RQ3_0 || dst->type == GGML_TYPE_RQ4_0 || dst->type == GGML_TYPE_RQ3_ISO || dst->type == GGML_TYPE_RQ4_ISO)) {
        static const char * env = getenv("RQ_INNERQ");
        if (env && atoi(env) > 0) {
            rq_innerq_init();
            rq_innerq_start_calibration();
            innerq_state = 1;
            fprintf(stderr, "InnerQ: calibration started (collecting %d tokens)\n", INNERQ_CALIBRATION_TOKENS);
        } else {
            rq_innerq_init(); // identity scales
            innerq_state = -1;
        }
    }

    // Track calibration progress
    if (innerq_state == 1 && (dst->type == GGML_TYPE_RQ3_0 || dst->type == GGML_TYPE_RQ4_0 || dst->type == GGML_TYPE_RQ3_ISO || dst->type == GGML_TYPE_RQ4_ISO)) {
        innerq_tokens_seen += dst->src[0]->ne[1];
        if (innerq_tokens_seen >= INNERQ_CALIBRATION_TOKENS) {
            rq_innerq_finalize_calibration();
            innerq_state = 2;
            fprintf(stderr, "InnerQ: calibration complete, scales active\n");
        }
    }

    if (src1->type == GGML_TYPE_I64) {
        set_rows_cuda<float, int64_t>(ctx, src0, src1, dst);
    } else {
        set_rows_cuda<float, int32_t>(ctx, src0, src1, dst);
    }
}
